pipeline {
    agent any

    parameters {
        string(name: 'CHANGED_SERVICES', defaultValue: '', description: 'Services that have new Docker images to deploy.')
        string(name: 'BUILD_NUMBER', defaultValue: '', description: 'Build number from the CI pipeline.')
    }

    stages {
        stage('Checkout DevOps Repo') {
            steps {
                checkout([
                    $class: 'GitSCM',
                    branches: [[name: '*/main']],
                    userRemoteConfigs: [[
                        url: 'https://github.com/noambenm/Skubestore-devops.git'
                    ]]
                ])
            }
        }

        stage('Determine Changed K8s Manifests') {
            steps {
                script {
                    env.CHANGED_K8S_FILES = ''
                    def changedFilesRaw = sh(
                        script: """
                          git diff --diff-filter=AMR HEAD^ HEAD --name-only | grep '^k8s/' || true
                        """,
                        returnStdout: true
                    ).trim()
                    if (changedFilesRaw) {
                        def changedFilesList = changedFilesRaw.split('\n')
                        echo "Changed K8s files: ${changedFilesList}"
                        env.CHANGED_K8S_FILES = changedFilesList.join(',')
                    } else {
                        echo 'No changed K8s files detected.'
                    }
                }
            }
        }

        stage('Apply Changed K8s Manifests') {
            when {
                expression {
                    return env.CHANGED_K8S_FILES?.trim()
                }
            }
            steps {
                script {
                    def changedK8sList = env.CHANGED_K8S_FILES.split(',')
                    changedK8sList.each { file ->
                        echo "Applying: ${file}"
                        sh "kubectl apply -f ${file}"
                    }
                }
            }
        }

        stage('Set Deployment Images for Changed Services') {
            when {
                expression { return params.CHANGED_SERVICES?.trim() }
            }
            steps {
                script {
                    def services = params.CHANGED_SERVICES.split(',')
                    services.each { service ->
                        def deploymentName = service.replace('-service', '-deployment')
                        def containerName  = service.replace('-service', '-container')
                        def imageTag = "noambenm/skubestore-${service}:v1.0.${params.BUILD_NUMBER}"
                        echo "Updating deployment/${deploymentName} container=${containerName} to ${imageTag}"
                        sh "kubectl config set-context --current --namespace=skubestore-stage"
                        sh """
                          kubectl set image \
                            deployment/${deploymentName} \
                            ${containerName}=${imageTag}
                        """
                        sh "kubectl rollout status deployment/${deploymentName} --timeout=60s"
                        sh "kubectl config set-context --current --namespace=skubestore-prod"
                        sh """
                          kubectl set image \
                            deployment/${deploymentName} \
                            ${containerName}=${imageTag}
                        """
                        sh "kubectl rollout status deployment/${deploymentName} --timeout=60s"
                    }
                }
            }
        }
    }

    post {
        always {
            echo "Finished pipeline (DevOps CD)."
        }
        success {
            echo "All changes successfully applied."
        }
        failure {
            echo "Deployment failed."
        }
    }
}
